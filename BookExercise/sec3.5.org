* Exercise 3.50
Complete the following definition, which generalizes stream-map to allow procedures that take multiple arguments, analogous to map in section 2.2.3, footnote 12.
** Answer
#+BEGIN_SRC scheme
(define (stream-map proc . argstreams)
  (if (stream-null? (car argstreams))
      the-empty-stream
      (cons
       (apply proc (map (lambda (s) (stream-car s)) argstreams))
       (memo-proc (lambda ()
                    (apply stream-map
                           (cons proc (map (lambda (s) (stream-cdr s))
                                         argstreams))))))))
#+END_SRC
* Exercise 3.51
In order to take a closer look at delayed evaluation, we will use the following procedure, which simply returns its argument after printing it:
#+BEGIN_SRC scheme
(define (show x)
  (display-line x)
  x)
#+END_SRC
What does the interpreter print in response to evaluating each expression in the following sequence?
#+BEGIN_SRC scheme
(define x (stream-map show (stream-enumerate-interval 0 10)))
(stream-ref x 5)
(stream-ref x 7)
#+END_SRC

** Answer
#+BEGIN_EXAMPLE
0
(0 . #<procedure (?)>)
---
1
2
3
4
---
5
6
#+END_EXAMPLE

* Exercise 3.52
Consider the sequence of expressions
#+BEGIN_SRC scheme
(define sum 0)
(define (accum x)
  (set! sum (+ x sum))
  sum)
(define seq (stream-map accum (stream-enumerate-interval 1 20)))
(define y (stream-filter even? seq))
(define z (stream-filter (lambda (x) (= (remainder x 5) 0))
seq))
(stream-ref y 7)
(display-stream z)
#+END_SRC
What is the value of sum after each of the above expressions is evaluated? What is the printed response to evaluating the stream-ref and display-stream expressions? Would these responses differ if we had implemented (delay <exp>) simply as (lambda () <exp>) without using the optimization provided by memo-proc ? Explain.

** Answer
#+BEGIN_EXAMPLE
0
---
#<procedure (accum)>
---
(1 . #<procedure (?)>)
---
(6 . #<procedure (?)>)
---
(10 . #<procedure (?)>)
---
(66 . #<procedure (?)>)
---
136
---
(10 15 45 55 105 120 190 210)
#+END_EXAMPLE
* Exercise 3.53
Without running the program, describe the elements of the stream defined by
#+BEGIN_SRC scheme
(define s (cons-stream 1 (add-streams s s)))
#+END_SRC
** Answers
Geometric series of 2^{n} (n=0,1,2,...)
* Exercise 3.54
Define a procedure mul-streams, analogous to add-streams, that produces the elementwise product of its two input streams. Use this together with the stream of integers to complete the following definition of the stream whose nth element (counting from 0) is n + 1 factorial:
#+BEGIN_SRC scheme
(define factorials (cons-stream 1 (mul-streams <??> <??>)))
#+END_SRC

** Answer
#+BEGIN_SRC scheme
(define factorials (cons 1 (memo-proc (lambda ()
                                        (stream-multiply (intsinitwith 2)
                                                         factorials)))))
#+END_SRC

* Exercise 3.55
Define a procedure partial-sums that takes as argument a stream S and returns the stream whose elements are S0, S0 + S1, S0 + S1 + S2, .... For example, (partial-sums integers) should be the stream 1, 3, 6, 10, 15, ....
** Answer
#+BEGIN_SRC scheme
(define partial-sums (cons 0 (memo-proc (lambda ()
                                          (stream-plus (intsinitwith 1)
                                                       partial-sums)))))
#+END_SRC

* Exercise 3.56
A famous problem, first raised by R. Hamming, is to enumerate, in ascending order with no repetitions, all positive integers with no prime factors other than 2, 3, or 5. One obvious way to do this is to simply test each integer in turn to see whether it has any factors other than 2, 3, and 5. But this is very inefficient, since, as the integers get larger, fewer and fewer of them fit the requirement. As an alternative, let us call the required stream of numbers S and notice the following facts about it.
1. S begins with 1.
2. The elements of (scale-stream S 2) are also elements of S.
3. The same is true for (scale-stream S 3) and (scale-stream 5 S).
4. These are all the elements of S.
Now all we have to do is combine elements from these sources. For this we define a procedure merge that combines two ordered streams into one ordered result stream, eliminating repetitions:
#+BEGIN_SRC scheme
(define (merge s1 s2)
  (cond ((stream-null? s1) s2)
        ((stream-null? s2) s1)
        (else
        ￼￼(let ((s1car (stream-car s1))
                (s2car (stream-car s2)))
            (cond ((< s1car s2car)
                   (cons-stream s1car (merge (stream-cdr s1) s2)))
                  ((> s1car s2car)
                   (cons-stream s2car (merge s1 (stream-cdr s2))))
                  (else
                   (cons-stream s1car
                                (merge (stream-cdr s1)
                                       (stream-cdr s2)))))))))
#+END_SRC
Then the required stream may be constructed with merge, as follows:
#+BEGIN_SRC scheme
(define S (cons-stream 1 (merge <??> <??>)))
#+END_SRC
Fill in the missing expressions in the places marked <??> above.
** Answer
*** Original Methods
#+BEGIN_SRC scheme
(define S (cons 1 (memo-proc (lambda ()
                               (merge (stream-scale S 2)
                                      (merge (stream-scale S 3)
                                             (stream-scale S 5)))))))
#+END_SRC
*** Make different merge
The text gives only merge between two streams, let's expand it to multiple first.
#+BEGIN_SRC scheme
(define (stream-merge order same? . streams)
  ;; (write "all streams:")
  ;; (print streams)
  (cond ((null? streams) the-empty-stream)
        ((let* ((non-null-streams (filter (lambda (s) (not (stream-null? s)))
                                          streams))
                ;; (tmp (begin (write "non-null-streams:")
                ;;             (print non-null-streams)))
                (chosen-element (apply order (map stream-car non-null-streams))))
           ;; (write "chosen element:")
           ;; (print chosen-element)
           ;; (print "ok")
           (cons chosen-element (memo-proc (lambda ()
                                             (let ((rest-streams (foundmap (lambda (s) (same? chosen-element (stream-car s)))
                                                                           cons stream-cdr the-empty-stream non-null-streams)))
                                               (apply stream-merge (cons order (cons same? rest-streams)))))))))))
#+END_SRC

But this merge does not remove the redundant elements. So construct a remove redundant operator.
#+BEGIN_SRC scheme
(define (stream-remove-redundant stream same?)
  (cons (stream-car stream)
        (memo-proc (lambda ()
                     (stream-remove-redundant (stream-filter (lambda (n) (not (same? n (stream-car stream))))
                                                             (stream-cdr stream))
                                              same?)))))
#+END_SRC

So the result is simply:
#+BEGIN_SRC scheme
(define s (stream-remove-redundant (cons 1 (memo-proc (lambda ()
                                                        (stream-merge min
                                                                      =
                                                                      (stream-scale s 2)
                                                                      (stream-scale s 3)
                                                                      (stream-scale s 5)))))
                                   =))
#+END_SRC
* Exercise 3.57
How many additions are performed when we compute the n^{th} Fibonacci number using the definition of fibs based on the add-streams procedure? Show that the number of additions would be exponentially greater if we had implemented =(delay <exp>)= simply as =(lambda () <exp>)=, without using the optimization provided by the memo-proc procedure described in section.
** Answer
It takes just \Theta(n) to compute the n^{th} Fibonacci number using the memorized lazy evaluation. However, if not memorized, f_{n} = f_{n-1} + f_{n-2}, thus it f_{n} = \Theta(c^{n}), using the characteristic function.
* Exercise 3.58
Give an interpretation of the stream computed by the following procedure:
#+BEGIN_SRC scheme
(define (expand num den radix)
  (cons-stream
   (quotient (* num radix) den)
   (expand (remainder (* num radix) den) den radix)))
#+END_SRC
(Quotient is a primitive that returns the integer quotient of two integers.) What are the successive elements produced by =(expand 1 7 10)= ? What is produced by =(expand 3 8 10)=?
** Answer
They are =(expand 3 7 10)= and =(expand 6 8 10)=.
* Exercise 3.59
In section 2.5.3 we saw how to implement a polynomial arithmetic system representing polynomials as lists of terms. In a similar way, we can work with power series, such as whose elements are the coefficients a0, a1, a2, a3, ....

a. The integral of the series a0 + a1 x + a2 x2 + a3 x3 +···is the series where c is any constant. Define a procedure integrate-series that takes as input a stream a0, a1, a2, ... representing a power series and returns the stream a0, (1/2)a1, (1/3)a2, ... of coefficients of the non-constant terms of the integral of the series. (Since the result has no constant term, it doesn't represent a power series; when we use integrate-series, we will cons on the appropriate constant.)
b. The function x ￼ ex is its own derivative. This implies that ex and the integral of ex are the same series, except for the constant term, which is e0 = 1. Accordingly, we can generate the series for ex as
#+BEGIN_SRC scheme
 (define exp-series
   (cons-stream 1 (integrate-series exp-series)))
#+END_SRC
Show how to generate the series for sine and cosine, starting from the facts that the derivative of sine is cosine and the derivative of cosine is the negative of sine:
#+BEGIN_SRC scheme
(define cosine-series
   (cons-stream 1 <??>))
(define sine-series
  (cons-stream 0 <??>))
(define exp-series
  (cons-stream 1 (integrate-series exp-series)))
#+END_SRC

** Answer
#+BEGIN_SRC scheme
(define exp-series (cons 1 (memo-proc (lambda ()
                                        (integrate-series exp-series)))))
(define cosine-series
  (cons 1 (memo-proc (lambda ()
                       (stream-scale (integrate-series sine-series)
                                     -1)))))
(define sine-series
  (cons 0 (memo-proc (lambda ()
                       (stream-scale (integrate-series cosine-series)
                                     -1)))))
#+END_SRC
* Exercise 3.60
With power series represented as streams of coefficients as in exercise 3.59, adding series is implemented by add-streams. Complete the definition of the following procedure for multiplying series:
#+BEGIN_SRC scheme
(define (mul-series s1 s2)
  (cons-stream <??> (add-streams <??> <??>)))
#+END_SRC
You can test your procedure by verifying that sin^{2}x + cos^{2}x = 1, using the series from exercise 3.59.
** Answer
#+BEGIN_SRC scheme
(define (mul-series s1 s2)
  (cons (* (stream-car s1)
           (stream-car s2))
        (memo-proc (lambda ()
                     (stream-plus (stream-scale (stream-cdr s1)
                                                (stream-car s2))
                                  (mul-series s1
                                              (stream-cdr s2)))))))
#+END_SRC

* Exercise 3.61
Let S be a power series (exercise 3.59) whose constant term is 1. Suppose we want to find the power series 1/S, that is, the series X such that S · X = 1. Write S = 1 + SR where SR is the part of S after the constant term. Then we can solve for X as following.
n other words, X is the power series whose constant term is 1 and whose higher-order terms are given by the negative of SR times X. Use this idea to write a procedure invert-unit-series that computes 1/S for a power series S with constant term 1. You will need to usemul-seriesfrom exercise 3.60.

** Answer
#+BEGIN_SRC scheme
(define (invert-unit-series stream)
  (cond ((= (stream-car stream) 0)
         (error "first element equal to 0 -- " (stream-car stream)))
        (else
         (let ((scalar (/ 1 (stream-car stream)))
               (stream-rest (stream-cdr stream))
               (invert-stream #f))
           (set! invert-stream (cons scalar
                                     (memo-proc (lambda ()
                                                  (stream-scale (mul-series stream-rest
                                                                            invert-stream)
                                                                (- scalar))))))
           invert-stream))))
#+END_SRC

* Exercise 3.62
Use the results of exercises 3.60 and 3.61 to define a procedure div-series that divides two power series. Div-series should work for any two series, provided that the denominator series begins with a nonzero constant term. (If the denominator has a zero constant term, then div-series should signal an error.) Show how to use div-series together with the result of exercise 3.59 to generate the power series for tangent.

** Answer
Division is just multiplication with the invert.
#+BEGIN_SRC scheme
(define (div-series s1 s2)
  (let ((invert-s2 (invert-unit-series s2)))
    (mul-series s1 invert-s2)))
#+END_SRC
* Exercise 3.63
Louis Reasoner asks why the sqrt-stream procedure was not written in the following more straightforward way, without the local variable guesses:
#+BEGIN_SRC scheme
(define (sqrt-stream x)
  (cons-stream 1.0
               (stream-map (lambda (guess)
                             (sqrt-improve guess x))
                           (sqrt-stream x))))
#+END_SRC
Alyssa P. Hacker replies that this version of the procedure is considerably less efficient because it performs redundant computation. Explain Alyssa's answer. Would the two versions still differ in efficiency if our implementation of delay used only (lambda () <exp>) without using the optimization provided by memo-proc (section 3.5.1)?

** Answer
The process uses a new =sqrt-stream= for map every time, thus it repeatedly recalculate the sequence again. So for one iteration, it needs to redo from the first x, wasting a lot of efficiency since the values are all memorized. It is of the same order of the growth as the non-memorized version of the lazy evaluation. We could proof it by:
#+BEGIN_SRC scheme
(define (sqrt-stream x)
  (let iter ((former 1.0))
    (cons-stream former
                 (iter (sqrt-improve former x)))))
#+END_SRC
* Exercise 3.64
Write a procedure stream-limit that takes as arguments a stream and a number (the tolerance). It should examine the stream until it finds two successive elements that differ in absolute value by less than the tolerance, and return the second of the two elements. Using this, we could compute square roots up to a given tolerance by
#+BEGIN_SRC scheme
(define (sqrt x tolerance)
  (stream-limit (sqrt-stream x) tolerance))
#+END_SRC

* Exercise 3.65
Use the series￼to compute three sequences of approximations to the natural logarithm of 2, in the same way we did above for \ln. How rapidly do these sequences converge?
** Answer
#+BEGIN_SRC scheme
(define (ln2-sum n)
  (cons (/ 1 n)
        (memo-proc (lambda ()
                     (stream-scale (ln2-sum (+ n 1))
                                   -1)))))

(define ln-sum-sequence (ln2-sum 1))

(define ln2-stream (stream-integral ln-sum-sequence
                                    0))
(define ln2-aitken (aitken-delte-square ln2-stream))
(define ln2-aitken-recursion (accelerate-stream ln2-stream aitken-delte-square))
#+END_SRC
* Exercise 3.66
Examine the stream (pairs integers integers). Can you make any general comments about the order in which the pairs are placed into the stream? For example, about how many pairs precede the pair (1,100)? the pair (99,100)? the pair (100,100)? (If you can make precise mathematical statements here, all the better. But feel free to give more qualitative answers if you find yourself getting bogged down.)
** Answer
It is easy to prove using the induction on k, and we have:
(s(k),t(k)) = T(2^{k+1}-2),
(s(k),t(k+l)) = T(2^{k+1}l+2^{k}-2),
(k > -1), (l > 0)

Thus:
(1,100) = (s(0),t(0+99)) = T(197)
(99,100) = (s(98),t(98+1)) = T(2^{98}3-2)
(100,100) = (s(99),t(99)) = T(2^{100}-2)

* Exercise 3.67
Modify the pairs procedure so that (pairs integers integers) will produce the stream of all pairs of integers (i,j) (without the condition i < j). Hint: You will need to mix in an additional stream.

** Answer
Still we will implement it as four parts: the first diagnoal element, the row followed, the column followed and the rest.

So, first the combination methods,
#+BEGIN_SRC scheme
(define (all-interleave first . rest)
  (cond ((null? rest) first)
        ((stream-null? first)
         (apply all-interleave rest))
        (else
         (cons (stream-car first)
               (memo-proc (lambda ()
                            (apply all-interleave (append rest (list (stream-cdr first))))))))))
#+END_SRC

Then, the pairs:
#+BEGIN_SRC scheme
(define (all-pairs s t)
  (cons (cons (stream-car s)
              (stream-car t))
        (memo-proc (lambda ()
                     (all-interleave (stream-map (lambda (m) (cons (stream-car s) m))
                                                 (stream-cdr t))
                                     (stream-map (lambda (n) (cons n (stream-car t)))
                                                 (stream-cdr s))
                                     (all-pairs (stream-cdr s)
                                                (stream-cdr t)))))))
#+END_SRC

* Exercise 3.68
Louis Reasoner thinks that building a stream of pairs from three parts is unnecessarily complicated. Instead of separating the pair (S0,T0) from the rest of the pairs in the first row, he proposes to work with the whole first row, as follows:
#+BEGIN_SRC scheme
(define (pairs s t)
  (interleave
   (stream-map (lambda (x) (list (stream-car s) x))
               t)
(pairs (stream-cdr s) (stream-cdr t))))
#+END_SRC
Does this work? Consider what happens if we evaluate (pairs integers integers) using Louis's definition of pairs.

** Answer
As the applicative order, the scheme will go into pairs again and again with infinity looping.

* Exercise 3.69
Write a procedure triples that takes three infinite streams, S, T, and U, and produces the stream of triples (Si,Tj,Uk) such that i < j < k. Use triples to generate the stream of all Pythagorean triples of positive integers, i.e., the triples (i,j,k) such that i < j and i2 + j2 = k2.
** Answer
We use the =ordered-pairs= implemented in the book, thus
#+BEGIN_SRC scheme
(define (ordered-triplet s t u)
  (cons (cons (stream-car s)
              (cons (stream-car t)
                    (stream-car u)))
        (memo-proc (lambda ()
                     (all-interleave (stream-map (lambda (x) (cons (stream-car s)
                                                                   x))
                                                 (stream-cdr (ordered-pairs t u)))
                                     (ordered-triplet (stream-cdr s)
                                                      (stream-cdr t)
                                                      (stream-cdr u)))))))
#+END_SRC

#+BEGIN_SRC scheme
(define pythogorean-triple (stream-filter (lambda (s) (let ((f (car s))
                                                            (s (cadr s))
                                                            (t (cddr s)))
                                                        (= (square t) (+ (square f)
                                                                         (square s)))))
                                          tri-ordered-ints))
#+END_SRC

* Exercise 3.70
It would be nice to be able to generate streams in which the pairs appear in some useful order, rather than in the order that results from an ad hoc interleaving process. We can use a technique similar to the merge procedure of exercise 3.56, if we define a way to say that one pair of integers is ``less than'' another. One way to do this is to define a ``weighting function'' W(i,j) and stipulate that (i1,j1) is less than (i2,j2) if W(i1,j1) < W(i2,j2). Write a procedure merge-weighted that is like merge, except that merge-weighted takes an additional argument weight, which is a procedure that computes the weight of a pair, and is used to determine the order in which elements should appear in the resulting merged stream. Using this, generalize pairs to a procedure weighted-pairs that takes two streams, together with a procedure that computes a weighting function, and generates the stream of pairs, ordered according to weight. Use your procedure to generate
a. the stream of all pairs of positive integers (i,j) with i < j ordered according to the sum i + j
b. the stream of all pairs of positive integers (i,j) with i < j, where neither i nor j is divisible by 2, 3, or 5, and the pairs are ordered according to the sum 2 i + 3 j + 5 i j.

** Answer
The idea is similar, we just need to compare the streams in a certain order if the elements are not number.
#+BEGIN_SRC scheme
(define (stream-compare-merge rank . streams)
  (cond ((null? streams) the-empty-stream)
        (else
         (let ((ordered-streams (ext-quick-sort streams (lambda (x)
                                                          (rank (stream-car x))))))
           (cons (stream-car (car ordered-streams))
                 (memo-proc (lambda ()
                              (apply stream-compare-merge (cons rank (cons (stream-cdr (car ordered-streams))
                                                                           (cdr ordered-streams)))))))))))

(define pair-rank (lambda (ele) (+ (car ele) (cdr ele))))

(define (ranked-pairs s t rank)
  (cons (cons (stream-car s)
              (stream-car (stream-cdr t)))
        (memo-proc (lambda ()
                     (stream-compare-merge rank
                                           (stream-map (lambda (x) (cons (stream-car s) x))
                                                       (stream-cdr (stream-cdr t)))
                                           (ranked-pairs (stream-cdr s)
                                                               (stream-cdr t)
                                                               rank))))))

;; the desired pair
(define desired-pair (stream-filter (lambda (s) (let ((f (car s))
                                                      (s (cdr s)))
                                                  (all (append (map (lambda (x) (not (divide? f x)))
                                                                    (list 2 3 5))
                                                               (map (lambda (x) (not (divide? s x)))
                                                                    (list 2 3 5))))))
                                    (ranked-pairs ints ints (lambda (ele) (+ (* 2 (car ele))
                                                                                   (* 3 (cdr ele))
                                                                                   (* 5 (car ele)
                                                                                      (cdr ele)))))))

#+END_SRC

* Exercise 3.71
Numbers that can be expressed as the sum of two cubes in more than one way are sometimes called Ramanujan numbers, in honor of the mathematician Srinivasa Ramanujan.70 Ordered streams of pairs provide an elegant solution to the problem of computing these numbers. To find a number that can be written as the sum of two cubes in two different ways, we need only generate the stream of pairs of integers (i,j) weighted according to the sum i3 + j3 (see exercise 3.70), then search the stream for two consecutive pairs with the same weight. Write a procedure to generate the Ramanujan numbers. The first such number is 1,729. What are the next five?

** Answer
#+BEGIN_SRC scheme
(define (cube-sum ele)
  (+ (cube (car ele))
     (cube (cdr ele))))

(define ordered-by-cube-sum (ranked-pairs ints ints cube-sum))

(define (remanujan-stream stream lastpairs sum)
  (cond ((null? lastpairs) (remanujan-stream (stream-cdr stream)
                                             (list (stream-car stream))
                                             (cube-sum (stream-car stream))))
        ((= sum
            (cube-sum (stream-car stream)))
         (remanujan-stream (stream-cdr stream)
                           (cons (stream-car stream)
                                 lastpairs)
                           sum))
        ((> (length lastpairs) 1)
         (cons (cons sum lastpairs)
               (memo-proc (lambda ()
                            (remanujan-stream (stream-cdr stream)
                                              (list (stream-car stream))
                                              (cube-sum (stream-car stream)))))))
        (else
         (remanujan-stream (stream-cdr stream)
                           (list (stream-car stream))
                           (cube-sum (stream-car stream))))))

(define rs (remanujan-stream ordered-by-cube-sum (list) 0))
(display-stream-ref rs 4)
#+END_SRC

The result is:
#+BEGIN_EXAMPLE
(1729 (9 . 10) (1 . 12))
(4104 (9 . 15) (2 . 16))
(13832 (18 . 20) (2 . 24))
(20683 (19 . 24) (10 . 27))
(32832 (18 . 30) (4 . 32))
#+END_EXAMPLE
* Exercise 3.72
In a similar way to exercise 3.71 generate a stream of all numbers that can be written as the sum of two squares in three different ways (showing how they can be so written).
** Answer
We could write a higher ordered procedure to select consecutive elements in a stream according to some requirements and the minimum length.
#+BEGIN_SRC scheme
(define (square-sum ele)
  (+ (square (car ele))
     (square (cdr ele))))

(define ordered-by-square-sum (ranked-pairs ints ints square-sum))

(define (select-length-pairs len stream rank)
  (let iter ((stream stream)
             (lastpairs (list))
             (sum #f))
    (cond ((null? lastpairs)
           (iter (stream-cdr stream)
                 (list (stream-car stream))
                 (rank (stream-car stream))))
          ((= sum
              (rank (stream-car stream)))
           (iter (stream-cdr stream)
                 (cons (stream-car stream)
                       lastpairs)
                 sum))
          ((>= (length lastpairs) len)
           (cons (cons sum lastpairs)
                 (memo-proc (lambda ()
                              (iter (stream-cdr stream)
                                    (list (stream-car stream))
                                    (rank (stream-car stream)))))))
          (else
           (iter (stream-cdr stream)
                 (list (stream-car stream))
                 (rank (stream-car stream)))))))

(define triple-square-sum (select-length-pairs 3 ordered-by-square-sum square-sum))
;; (display-stream-ref triple-square-sum 1)
;; (exit)
#+END_SRC

And the starting two are
#+BEGIN_EXAMPLE
(325 (10 . 15) (6 . 17) (1 . 18))
(425 (13 . 16) (8 . 19) (5 . 20))
#+END_EXAMPLE

* Exercise 3.73
Write a procedure RC that models this circuit. RC should take as inputs the values of R, C, and dt and should return a procedure that takes as inputs a stream representing the current i and an initial value for the capacitor voltage v0 and produces as output the stream of voltages v. For example, you should be able to use RC to model an RC circuit with R = 5 ohms, C = 1 farad, and a 0.5-second time step by evaluating (define RC1 (RC 5 1 0.5)). This defines RC1 as a procedure that takes a stream representing the time sequence of currents and an initial capacitor voltage and produces the output stream of voltages.

#+BEGIN_SRC scheme
(define (RC r c dt)
  (lambda (v0 i)
    (stream-map +
                (constant-stream v0)
                (stream-scale (stream-integral-dt i 0 dt)
                              (/ 1 c))
                (stream-scale i r))))

(define rc1 (RC 5 1 0.5))
;; (display-stream-ref (rc1 0 ones) 10)
;; (exit)
#+END_SRC
* Exercise 3.74
Alyssa P. Hacker is designing a system to process signals coming from physical sensors. One important feature she wishes to produce is a signal that describes the zero crossings of the input signal. That is, the resulting signal should be + 1 whenever the input signal changes from negative to positive, - 1 whenever the input signal changes from positive to negative, and 0 otherwise. (Assume that the sign of a 0 input is positive.) For example, a typical input signal with its associated zero-crossing signal would be In Alyssa's system, the signal from the sensor is represented as a stream sense-data and the stream zero-crossings is the corresponding stream of zero crossings. Alyssa first writes a procedure sign- change-detector that takes two values as arguments and compares the signs of the values to produce an appropriate 0, 1, or - 1. She then constructs her zero-crossing stream as follows:
#+BEGIN_SRC scheme
(define (make-zero-crossings input-stream last-value)
  (cons-stream
   (sign-change-detector (stream-car input-stream) last-value)
   (make-zero-crossings (stream-cdr input-stream)
                        (stream-car input-stream))))
(define zero-crossings (make-zero-crossings sense-data 0))
#+END_SRC
Alyssa's boss, Eva Lu Ator, walks by and suggests that this program is approximately equivalent to the following one, which uses the generalized version of stream-map from exercise 3.50:
#+BEGIN_SRC scheme
(define zero-crossings
  (stream-map sign-change-detector sense-data <expression>))
#+END_SRC
Complete the program by supplying the indicated <expression>.
** Answer
#+BEGIN_SRC scheme
(define zero-crossings
  (stream-map sign-change-detector sense-data (stream-cons 0 sense-data)))
#+END_SRC
* Exercise 3.75
Unfortunately, Alyssa's zero-crossing detector in exercise 3.74 proves to be insufficient, because the noisy signal from the sensor leads to spurious zero crossings. Lem E. Tweakit, a hardware specialist, suggests that Alyssa smooth the signal to filter out the noise before extracting the zero crossings. Alyssa takes his advice and decides to extract the zero crossings from the signal constructed by averaging each value of the sense data with the previous value. She explains the problem to her assistant, Louis Reasoner, who attempts to implement the idea, altering Alyssa's program as follows:
#+BEGIN_SRC scheme
(define (make-zero-crossings input-stream last-value)
  (let ((avpt (/ (+ (stream-car input-stream) last-value) 2)))
￼￼(cons-stream (sign-change-detector avpt last-value)
(make-zero-crossings (stream-cdr input-stream)
                                      avpt))))
#+END_SRC
This does not correctly implement Alyssa's plan. Find the bug that Louis has installed and fix it without changing the structure of the program. (Hint: You will need to increase the number of arguments tomake- zero-crossings.)

** Answer
#+BEGIN_SRC scheme
(define (make-zero-crossings input-stream last-value last-average)
  (let ((avpt (/ (+ (stream-car input-stream) last-value) 2)))
    (cons-stream (sign-change-detector avpt last-average)
                 (make-zero-crossings (stream-cdr input-stream)
                                      (stream-car input-stream)
                                      avpt))))
#+END_SRC
* Exercise 3.76
Eva Lu Ator has a criticism of Louis's approach in exercise 3.75. The program he wrote is not modular, because it intermixes the operation of smoothing with the zero-crossing extraction. For example, the extractor should not have to be changed if Alyssa finds a better way to condition her input signal. Help Louis by writing a procedure smooth that takes a stream as input and produces a stream in which each element is the average of two successive input stream elements. Then use smooth as a component to implement the zero-crossing detector in a more modular style.
** Answer
#+BEGIN_SRC scheme
(define (smooth-streams method . streams)
  (cond ((null? streams) the-empty-stream)
        (else
         (cons (apply method (map (lambda (x) (stream-car x))
                                  streams))
               (memo-proc (lambda ()
                            (apply smooth-streams (cons method (map (lambda (x) (stream-cdr x))
                                                                    streams)))))))))

(define (arma n)
  (lambda (input-stream)
    (apply smooth-streams (cons average (cons input-stream
                                              (let iter ((number (- n 1))
                                                         (current (stream-cdr input-stream)))
                                                (cond ((= number 0) (list))
                                                      (else (cons current (iter (- number 1)
                                                                                (stream-cdr current)))))))))))
#+END_SRC
* Exercise 3.77
The integral procedure used above was analogous to the ``implicit'' definition of the infinite stream of integers in section 3.5.2. Alternatively, we can give a definition of integral that is more like integers-starting-from (also in section 3.5.2):
#+BEGIN_SRC scheme
(define (integral integrand initial-value dt)
  (cons-stream initial-value
               (if (stream-null? integrand)
                   the-empty-stream
                   (integral (stream-cdr integrand)
                             (+ (* dt (stream-car integrand))
                                initial-value)
                             dt))))
#+END_SRC
When used in systems with loops, this procedure has the same problem as does our original version of integral. Modify the procedure so that it expects the integrand as a delayed argument and hence can be used in the solve procedure shown above.

** Answer
#+BEGIN_SRC scheme
(define (integral integrand initial-value dt)
  (cons-stream initial-value
               (let ((real-int (force integrand)))
                 (if (stream-null? real-int)
                   the-empty-stream
                   (integral (stream-cdr real-int)
                             (+ (* dt (stream-car real-int))
                                initial-value)
                             dt)))))
#+END_SRC

* Exercise 3.78
Consider the problem of designing a signal-processing system to study the homogeneous second-order linear differential equation The output stream, modeling y, is generated by a network that contains a loop. This is because the value of d2y/dt2 depends upon the values of y and dy/dt and both of these are determined by integrating d2y/dt2. The diagram we would like to encode is shown in figure 3.35. Write a procedure solve-2nd that takes as arguments the constants a, b, and dt and the initial values y0 and dy0 for y and dy/dt and generates the stream of successive values of y.

** Answer
#+BEGIN_SRC scheme
(define (solve-2nd y0 dy0 a b dt)
  (let ((y #f)
        (dy #f)
        (d2y #f))
    (set! y (stream-integral-dt-delay (lambda () dy) y0 dt))
    (set! dy (stream-integral-dt-delay (lambda () d2y) dy0 dt))
    (set! d2y (stream-map (lambda (x y) (+ (* a x)
                                           (* b y)))
                          dy
                          y))
    y))
#+END_SRC

* Exercise 3.79
Generalize the solve-2nd procedure of exercise 3.78 so that it can be used to solve general second-order differential equations d2 y/dt2 = f(dy/dt, y).
** Answer
#+BEGIN_SRC scheme
(define (sovle-2nd y0 dy0 f dt)
  (let ((y #f)
        (dy #f)
        (d2y #f))
    (set! y (stream-integral-dt-delay (lambda () dy) y0 dt))
    (set! dy (stream-integral-dt-delay (lambda () d2y) dy0 dt))
    (set! d2y (stream-map (lambda (x y) (f x y))
                          dy
                          y))
    y))
#+END_SRC


* Exercise 3.80
Write a procedure RLC that takes as arguments the parameters R, L, and C of the circuit and the time increment dt. In a manner similar to that of the RC procedure of exercise 3.73, RLC should produce a procedure that takes the initial values of the state variables, vC0 and iL0, and produces a pair (using cons) of the streams of states vC and iL. Using RLC, generate the pair of streams that models the behavior of a series RLC circuit with R = 1 ohm, C = 0.2 farad, L = 1 henry, dt = 0.1 second, and initial values iL0 = 0 amps and vC0 = 10 volts.

** Answer
#+BEGIN_SRC scheme
(define (rlc r l c dt)
  (lambda (i0 v0)
    (let ((i #f)
          (v #f))
      (set! v (stream-integral-dt-delay (lambda () (stream-scale i (- (/ 1 c))))
                                        v0
                                        dt))
      (set! i (stream-integral-dt-delay (lambda () (stream-plus (stream-scale v (/ 1 l))
                                                                (stream-scale i (- (/ r l)))))
                                        i0
                                        dt))
      (cons v i))))
#+END_SRC
